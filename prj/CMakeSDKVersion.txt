# SDK version
# This file is part of NAppGUI-SDK project
# See README.txt and LICENSE.txt

set(NAPPGUI_MAJOR 1)
set(NAPPGUI_MINOR 2)
set(NAPPGUI_REVISION 0)

# Build version
#------------------------------------------------------------------------------
# This is a 'Release' distribution
if (EXISTS ${CMAKE_PRJ_PATH}/build.txt)
    set(VERSION_FILE ${CMAKE_PRJ_PATH}/build.txt)
    set(RELEASE_DISTRIBUTION YES)
    if (EXISTS "${VERSION_FILE}")
        file(READ ${VERSION_FILE} NAPPGUI_BUILD)
        string(REPLACE "\r\n" "" NAPPGUI_BUILD ${NAPPGUI_BUILD})
        string(REPLACE "\n" "" NAPPGUI_BUILD ${NAPPGUI_BUILD})
    endif()

# This a 'Development' version
else()

    set(SVNVERSION "svnversion")
    
    # Xcode 11 doesn't install 'svn'
    # https://stackoverflow.com/questions/60869347/mac-command-line-tools-11-4-no-longer-has-svn
    # We use 'brew install svn'
    if(XCODE_VERSION VERSION_GREATER 10.99)
        set(SVNVERSION "/usr/local/bin/svnversion")
    endif()
    
    execute_process(COMMAND "${SVNVERSION}" "-n" WORKING_DIRECTORY ${CMAKE_PRJ_PATH} OUTPUT_VARIABLE SVN_VERSION ERROR_VARIABLE SVN_ERROR)
    if (NOT ${SVN_VERSION} STREQUAL "")
        string(FIND ${SVN_VERSION} ":" SVN_POS)
        if (NOT ${SVN_POS} EQUAL -1)
            math(EXPR SVN_POS "${SVN_POS} + 1")
            string(SUBSTRING ${SVN_VERSION} ${SVN_POS} -1 SVN_VERSION)
        endif()

        string(REPLACE "M" "" SVN_VERSION ${SVN_VERSION})
        string(REPLACE "S" "" SVN_VERSION ${SVN_VERSION})
        string(REPLACE "P" "" SVN_VERSION ${SVN_VERSION})
        set(NAPPGUI_BUILD ${SVN_VERSION})

    else()
        message("- Unknown NAppGUI Revision")
        set(NAPPGUI_BUILD "")
        
    endif()

endif()
